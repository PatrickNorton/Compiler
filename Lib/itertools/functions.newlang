from types typeget Boolable, Callable, Iterable

export all, any, count, countEqual, countFalse, countNotEqual, first, last


func all(Iterable[Boolable] values) -> bool {
    for var i in values {
        return false if not i
    }
    return true
}


func any(Iterable[Boolable] values) -> bool {
    for var i in values {
        return true if i
    }
    return false
}


generic T
func count(Callable[T, Boolable] predicate = bool, Iterable[T] values, /) -> uint {
    return \+(1 for T i in values if predicate(i))
}


generic T
func countEqual(T sentinel, Iterable[T] values, /) -> uint {
    return \+(1 for T i in values if i == sentinel)
}


generic T
func countFalse(Callable[T, Boolable] predicate = bool, Iterable[T] values, /) -> uint {
    return \+(1 for T i in values if not predicate(i))
}


generic T
func countNotEqual(T sentinel, Iterable[T] values, /) -> uint {
    return \+(1 for T i in values if i != sentinel)
}


generic T
func first(Callable[T, Boolable] predicate, Iterable[T] values, T fallback) -> T {
    for T value in values {
        return value if predicate(value)
    }
    return fallback
}


generic T
func last(Callable[T, Boolable] predicate, Iterable[T] values, T fallback) -> T {
    T lastSoFar = fallback
    for T value in values {
        if predicate(value) {
            lastSoFar = value
        }
    }
    return lastSoFar
}
