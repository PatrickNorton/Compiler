from types import Hashable, Array
from numtools import highestOneBit
from math import max

export HashMap

typedef VarInfo as Tuple[int64, K, V]

const int DEFAULT_ARRAY_SIZE = 1 << 4
const dec RESIZE_THRESHOLD = 0.75
const int PERTURB_SHIFT = 5

enum Empty {
    EMPTY,
    DELETED
}

class HashMap[K[Hashable], V] from dict[K, V] {
    private Array[VarInfo|Empty] values
    private uint length

    public operator new (*tuple[K, V] values) {
        int length = max(highestOneBit(values.length) << 1, DEFAULT_ARRAY_SIZE)
        self.internal = Array(length, Empty.EMPTY)
        self.valueList = []
        self.length = 0
        self.resize(values.length)
        for K key, V value in values {
            self[key] = value
        }
    }

    public operator [] (K key) -> V {
        uint index = self.getInfo(key)
        raise IndexError if self.values[index] is Empty.EMPTY
        return (self.values[index] casted VarInfo)[2]
    }

    public operator []= (K key, V value) {
        self.resize(self.length + 1)
        uint index = self.getInfo(key, returnDeleted=true)
        if self.values[index] instanceof Empty {
            self.length++
        }
        self.values[index] = (hash(key), key, value)
    }

    public operator del[] (K key) {
        int oldIndex = self.getInfo(key)
        raise KeyError if self.values[oldIndex] is Empty.EMPTY
        self.values[oldIndex] = Empty.DELETED
        self.length--
    }

    public operator in (K key) {
        return not (self.values[self.getInfo(key)] instanceof Empty)
    }

    public operator iter -> V {
        yield from ((i casted VarInfo)[2] for var i in self.valueList if not (i instanceof Empty))
    }

    public operator hash {
        return hash(f"dict[{K}, {V}]", *self)
    }

    public method remove(V value) -> K {
        del self[self.keyOf(value)]
    }

    public method keyOf(V value) -> K {
        for K key, V val in self {
            if value == val {
                return key
            }
        }
    }

    private method getInfo(K key, *, bool returnDeleted = false) -> uint {
        uint index = hash(key) % self.values.length
        while true {
            var value = self.values[index]
            return index if value is Empty.EMPTY or (returnDeleted and value is Empty.DELETED)
            continue if value is Empty.DELETED
            var v = value casted VarInfo
            return index if v[0] == hash(key) and v[1] == key
            index = (5*index + 1 + (hash(key) >> PERTURB_SHIFT)) % self.values.length
        }
    }

    private method resize(uint size) {
        return if size / self.values.length < RESIZE_THRESHOLD
        uint newSize = self.values.length << 1
        var old = self.values
        self.values = Array(newSize, Empty.EMPTY)
        for var v in (i casted VarInfo for var i in old if not (i instanceof Empty)) {
            self.values[self.getInfo(v[1], returnDeleted=true)] = v
        }
    }
}
