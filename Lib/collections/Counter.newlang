from itertools import chain, firstN, repeat

export Counter

final class Counter[T] from dict[T, int] {
    private final dict[T, int] values

    public property int length {
        get {
            return self.values.length
        }
    }

    public operator new (*T items) {
        self.values = {:}
        for T i in items {
            if i in self.values {
                self.values[i]++
            } else {
                self.values[i] = 1
            }
        }
    }

#     public operator new (dict[T, int] items) {
#         self.values = {i: j for var i, var j in items}
#     }

    public operator [] (T value) -> int {
        try {
            return self.values[value]
        } except KeyError {
            return 0
        }
    }

    # Methods that are simply delegated to self.values
    public mut operator []= = self.values.operator []=
    public mut operator del[] = self.values.operator del[]
    public operator iter = self.values.operator iter
    public operator in = self.values.operator in
    public operator == = self.values.operator ==
    public operator bool = self.values.operator bool

    public operator + (cls other) -> cls {
        return {i: self[i] + other[i] for T i in chain(self.keys(), other.keys())}
    }

    public operator repr {
        return f"{type(self)}({self.values})"
    }

    public generator method keys() -> T {
        yield from self.values.keys()
    }

    public mut method remove(int value) {
        self.values.remove(value)
    }

    public mut method keyOf(int value) -> T {
        return self.values.keyOf(value)
    }

    public generator method elements() -> T {
        for T value, int count in self {
            yield from repeat(value, count)
        }
    }

    public generator method mostCommon(int count = 0) -> T {
        var sorted = sorted(self.values.keys(), key=self.values.operator [])
        yield from firstN(count, sorted) if count else sorted
    }
}
